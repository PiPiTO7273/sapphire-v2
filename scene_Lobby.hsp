
*scene_Lobby_Init

	gosub*UploadAllSavedata

	dim SceneCount
	dim SceneTime
	sdim DecideJumpScene
	dim DecideTime
	dim AcceptGiftCodeFlag
	sdim InputGiftCode
	sdim AcceptAsciiTable
	AcceptAsciiTable="abcdefghijklmnopqrstuvwxyz0123456789"
	DecideTime=-1

	dim unlimited_dialogmode

	duio_InitAllCount"#Lobby"

	duio_SetActiveScene"#Dialog-Common-InvalidAction",FALSE

	duio_SetAllButtonVisible"#Lobby",TRUE
	duio_SetAllImageVisible"#Lobby",TRUE
	duio_SetAllTextVisible"#Lobby",TRUE

	duio_SetActiveScene"#Accept-GiftCode",FALSE
	duio_SetAllButtonVisible"#Accept-GiftCode",FALSE
	duio_SetAllImageVisible"#Accept-GiftCode",FALSE
	duio_SetAllTextVisible"#Accept-GiftCode",FALSE

	duio_SetActiveScene"#Response-GiftCode",FALSE
	duio_SetAllButtonVisible"#Response-GiftCode",FALSE
	duio_SetAllImageVisible"#Response-GiftCode",FALSE
	duio_SetAllTextVisible"#Response-GiftCode",FALSE

	if duio_GetImageOutroFlag(duio_ImageID2UIsetID("shutter_00"))!TRUE :duio_PlaySound"shutter_out"
	duio_SetAllImageOutro"#Shutter"

	//フィード
	sdim updfeed
	updfeed=strf("%04d.%d.%d %d.%02d.%02d 更新 |【最新のニュース】",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6))
	updfeed+PortalWebSvc_DownloadUpdateFeed()

	UpdateProfileDisp

	if BASS_ChannelIsActive(duio_GetHandle("main"))!BASS_ACTIVE_PLAYING {
		duio_PlaySound"main"
	}
	duio_SetVolume"main",1.0

	duio_PlaySound"voice_lobby"

	return

*scene_Lobby

	DrawCommonBackground FrameCount

	SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
	DrawGraph 0,0,hdximg(icommon_bglayer),TRUE

	//鉄骨ライン
		dim shapemovepos:shapemovepos=-(1000.0/duio_GetFPS()*FrameCount)/15
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph shapemovepos\1920,20,hdximg(icommon_ironline),TRUE
		DrawGraph shapemovepos\1920+1920,20,hdximg(icommon_ironline),TRUE

	duio_Draw"#Lobby"
	duio_Draw"#Common-Profile"

	//フィード
	if SceneTime>250 {
		setease BufHeight,1000,ease_cubic_out
		EasePosY=getease(SceneTime-250,300)
		setease 0,180,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneTime-250,250)
		DrawBox 0,EasePosY,BufWidth,EasePosY+40,$000000,TRUE
		setease 0,256,ease_cubic_out
		SetDrawBlendMode DX_BLENDMODE_ALPHA,getease(SceneTime-250,250)
		DrawStringToHandle BufWidth-((SceneTime+1000)/10)\(GetDrawStringWidthToHandle(updfeed,strlen(updfeed),duio_GetFontHandle("rom1cm20"))+BufWidth),EasePosY+8,updfeed,$FFFFFF,duio_GetFontHandle("rom1cm20")
	}

	duio_Draw"#Shutter"
	duio_Draw"#Dialog-Common-InvalidAction"
	duio_Draw"#Accept-GiftCode"
	duio_Draw"#Response-GiftCode"

	if AcceptGiftCodeFlag {
		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		repeat 10
			if cnt>strlen(InputGiftCode) :break
			if strmid(InputGiftCode,cnt,1)==""&SceneCount/4\2==0 {
				DrawStringXCenterToHandle 715+50*cnt,514,"_",40,$000000,duio_GetFontHandle("rom1cm24")
			} else {
				DrawStringXCenterToHandle 715+50*cnt,514,strmid(InputGiftCode,cnt,1),40,$000000,duio_GetFontHandle("rom1cm24")
			}
		loop
		repeat strlen(AcceptAsciiTable)
			if strlen(InputGiftCode)>=10 :break
			if IsPushed(ToDxlibKeyCode(peek(AcceptAsciiTable,cnt))) {
				duio_PlaySound"opchange"
				if peek(AcceptAsciiTable,cnt)>=97&peek(AcceptAsciiTable,cnt)<=122 {	//アルファベットを大文字化
					poke InputGiftCode,strlen(InputGiftCode),peek(AcceptAsciiTable,cnt)-32
				} else {
					poke InputGiftCode,strlen(InputGiftCode),peek(AcceptAsciiTable,cnt)
				}
			}
		loop
		if IsPushed(KEY_INPUT_BACK) :InputGiftCode=strmid(InputGiftCode,0,strlen(InputGiftCode)-1):duio_PlaySound"opchange"
		if IsPushed(KEY_INPUT_DELETE) :sdim InputGiftCode:duio_PlaySound"cancel"
		if IsPushed(KEY_INPUT_RETURN) :duio_PlaySound"decide":duio_SetEventStack"giftcode_ok"
	}

	//Unlimitedモード
	duio_Draw"#Dialog-Unlimited"
	duio_Draw"#Dialog-Unlimited-NoFragment"
	duio_Draw"#Dialog-Unlimited-Already"

	DecideTime+FrameTime

	//シャッター演出あり
	if DecideTime>1000 {
		switch DecideJumpScene
			case"freeplay"
				curSceneTag="#MusicSelect"
				gosub*scene_MusicSelect_Init
			swbreak
			case"tutorial"
				curSceneTag="#Tutorial"
				gosub*scene_Tutorial_Init
			swbreak
			case"title"
				curSceneTag="#Title"
				gosub*scene_Title_Init
			swbreak
			case"systemconfig"
				curSceneTag="#Config"
				gosub*scene_Config_Init
				DecideJumpScene="from:lobby"
			swbreak
		swend
	}
	//シャッター演出なし
	if DecideTime>300 {
		switch DecideJumpScene
			case"mission"
				gosub*scene_Mission_Init
				curSceneTag="#Mission"
			swbreak
			case"store"
				gosub*scene_Store_Init
				curSceneTag="#Store"
			swbreak
			case"profile"
				gosub*scene_Profile_Init
				curSceneTag="#Profile"
			swbreak
		swend
	}

	if duio_IsActiveScene("#Dialog-Common-InvalidAction")&IsPushed(KEY_INPUT_RETURN) :duio_SetEventStack"dialog_common_invalidaction_close"
	if duio_IsActiveScene("#Response-GiftCode")&IsPushed(KEY_INPUT_RETURN) :duio_SetEventStack"giftcode_res_ok":duio_PlaySound"decide"

	if duio_GetButtonEventStack()!"" :gosub*scene_Lobby_eventstack

	return

*scene_Lobby_eventstack

	switch duio_GetButtonEventStack()

		case"freeplay"
			gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="freeplay"
		swbreak

		case"mission"
			;gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="mission"
		swbreak

		case"store"
			;gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="store"
		swbreak

		case"profile"
			;gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="profile"
		swbreak

		case"unlimited"
			if UnlimitedTime==0 {
				//有効
				duio_SetActiveScene"#Lobby",FALSE
				duio_SetActiveScene"#Dialog-Unlimited",TRUE
				duio_InitAllCount"#Dialog-Unlimited"
				duio_SetAllButtonVisible"#Dialog-Unlimited",TRUE
				duio_SetAllImageVisible"#Dialog-Unlimited",TRUE
				duio_SetAllTextVisible"#Dialog-Unlimited",TRUE
			} else {
				//既に有効（エラー）
				duio_SetActiveScene"#Lobby",FALSE
				duio_SetActiveScene"#Dialog-Unlimited-Already",TRUE
				duio_InitAllCount"#Dialog-Unlimited-Already"
				duio_SetAllButtonVisible"#Dialog-Unlimited-Already",TRUE
				duio_SetAllImageVisible"#Dialog-Unlimited-Already",TRUE
				duio_SetAllTextVisible"#Dialog-Unlimited-Already",TRUE
			}
		swbreak

		case"unlimited_nofragment_ok"
			duio_SetActiveScene"#Lobby",TRUE
			duio_SetActiveScene"#Dialog-Unlimited-NoFragment",FALSE
			duio_SetAllButtonOutro"#Dialog-Unlimited-NoFragment"
			duio_SetAllImageOutro"#Dialog-Unlimited-NoFragment"
			duio_SetAllTextOutro"#Dialog-Unlimited-NoFragment"
		swbreak

		case"unlimited_already_ok"
			duio_SetActiveScene"#Lobby",TRUE
			duio_SetActiveScene"#Dialog-Unlimited-Already",FALSE
			duio_SetAllButtonOutro"#Dialog-Unlimited-Already"
			duio_SetAllImageOutro"#Dialog-Unlimited-Already"
			duio_SetAllTextOutro"#Dialog-Unlimited-Already"
		swbreak

		case"unlimited_5m"
			gosub*scene_Lobby_CloseUnlimited
			if pfreg_NumFragments<5 {
				gosub*scene_Lobby_OpenUnlimitedNoFragment
			} else {
				pfreg_NumFragments-5
				UnlimitedTime=5*60*1000
				UpdateProfileDisp
			}
		swbreak

		case"unlimited_10m"
			gosub*scene_Lobby_CloseUnlimited
			if pfreg_NumFragments<10 {
				gosub*scene_Lobby_OpenUnlimitedNoFragment
			} else {
				pfreg_NumFragments-10
				UnlimitedTime=10*60*1000
				UpdateProfileDisp
			}
		swbreak

		case"unlimited_15m"
			gosub*scene_Lobby_CloseUnlimited
			if pfreg_NumFragments<15 {
				gosub*scene_Lobby_OpenUnlimitedNoFragment
			} else {
				pfreg_NumFragments-15
				UnlimitedTime=15*60*1000
				UpdateProfileDisp
			}
		swbreak

		case"unlimited_30m"
			gosub*scene_Lobby_CloseUnlimited
			if pfreg_NumFragments<30 {
				gosub*scene_Lobby_OpenUnlimitedNoFragment
			} else {
				pfreg_NumFragments-30
				UnlimitedTime=30*60*1000
				UpdateProfileDisp
			}
		swbreak

		case"unlimited_cancel"
			gosub*scene_Lobby_CloseUnlimited
		swbreak

		case"tutorial"
			gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="tutorial"
		swbreak

		case"copyright"
			exec"http://piyastorage.ml/embdoc/sapphirev2/copyright/index.html",16
		swbreak
		case"openweb"
			exec"http://"+duio_idsGet("Server"),16
		swbreak

		case"systemconfig"
			gosub*onshutter
			DecideTime=0
			
			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="systemconfig"
		swbreak

		case"gift"
			sdim InputGiftCode
			duio_SetActiveScene"#Lobby",FALSE
			duio_SetActiveScene"#Accept-GiftCode",TRUE
			duio_InitAllCount"#Accept-GiftCode"
			duio_SetAllButtonVisible"#Accept-GiftCode",TRUE
			duio_SetAllImageVisible"#Accept-GiftCode",TRUE
			duio_SetAllTextVisible"#Accept-GiftCode",TRUE
			AcceptGiftCodeFlag=TRUE
		swbreak

		case"giftcode_ok"
			duio_SetActiveScene"#Accept-GiftCode",FALSE
			duio_SetAllButtonOutro"#Accept-GiftCode"
			duio_SetAllImageOutro"#Accept-GiftCode"
			duio_SetAllTextOutro"#Accept-GiftCode"
			AcceptGiftCodeFlag=FALSE

			ExecGiftCode InputGiftCode

			duio_SetActiveScene"#Response-GiftCode",TRUE
			duio_InitAllCount"#Response-GiftCode"
			duio_SetAllButtonVisible"#Response-GiftCode",TRUE
			duio_SetAllImageVisible"#Response-GiftCode",TRUE
			duio_SetAllTextVisible"#Response-GiftCode",TRUE
			switch stat
				case 0
					//正常終了
					duio_ChangeTextBody duio_TextID2UIsetID("giftcode_res_text"),refstr+"\n\nを獲得しました。"
				swbreak
				case 1
					//使用済み
					duio_ChangeTextBody duio_TextID2UIsetID("giftcode_res_text"),"\n入力されたギフトコードは既に使用されています。"
				swbreak
				case 2
					//存在しないコード
					duio_ChangeTextBody duio_TextID2UIsetID("giftcode_res_text"),"入力されたギフトコードは存在しません。\n\nもう一度正しく入力してください。"
				swbreak
				default
					//エラー
					duio_ChangeTextBody duio_TextID2UIsetID("giftcode_res_text"),"\n不明なエラーです。"
				swbreak
			swend
		swbreak

		case"giftcode_res_ok"
			duio_SetActiveScene"#Lobby",TRUE
			duio_SetActiveScene"#Response-GiftCode",FALSE
			duio_SetAllButtonOutro"#Response-GiftCode"
			duio_SetAllImageOutro"#Response-GiftCode"
			duio_SetAllTextVisible"#Response-GiftCode",FALSE
		swbreak

		case"giftcode_cancel"
			duio_SetActiveScene"#Lobby",TRUE
			duio_SetActiveScene"#Accept-GiftCode",FALSE
			duio_SetAllButtonOutro"#Accept-GiftCode"
			duio_SetAllImageOutro"#Accept-GiftCode"
			duio_SetAllTextOutro"#Accept-GiftCode"
			AcceptGiftCodeFlag=FALSE
		swbreak

		case"title"
			gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="title"
		swbreak

		case"signout"

			PlayerID="":PlayerPassword=""
			gosub*SaveClientData

			gosub*onshutter
			DecideTime=0

			duio_SetAllButtonOutro"#Lobby"
			duio_SetAllImageOutro"#Lobby"
			duio_SetAllTextOutro"#Lobby"

			DecideJumpScene="title"
		swbreak

		case"game.exit"
			goto*Exit
		swbreak

		case"dialog_common_invalidaction_close"
			duio_SetImageOutro duio_ImageID2UIsetID("dialog_common_invalidaction"),TRUE
			duio_SetButtonOutro duio_ButtonID2UIsetID("dialog_common_invalidaction_ok"),TRUE
			duio_SetButtonEnable duio_ButtonID2UIsetID("dialog_common_invalidaction_ok"),FALSE
			duio_SetActiveScene"#Lobby",TRUE
			duio_SetActiveScene"#Dialog-Common-InvalidAction",FALSE
		swbreak

		default
			duio_SetImageVisible duio_ImageID2UIsetID("dialog_common_invalidaction"),TRUE
			duio_SetButtonVisible duio_ButtonID2UIsetID("dialog_common_invalidaction_ok"),TRUE
			duio_SetButtonEnable duio_ButtonID2UIsetID("dialog_common_invalidaction_ok"),TRUE
			duio_SetActiveScene"#Lobby",FALSE
			duio_SetActiveScene"#Dialog-Common-InvalidAction",TRUE
		swbreak

	swend

	return

*scene_Lobby_CloseUnlimited
	duio_SetActiveScene"#Lobby",TRUE
	duio_SetActiveScene"#Dialog-Unlimited",FALSE
	duio_SetAllButtonOutro"#Dialog-Unlimited"
	duio_SetAllImageOutro"#Dialog-Unlimited"
	duio_SetAllTextOutro"#Dialog-Unlimited"
	return

*scene_Lobby_OpenUnlimitedNoFragment
	duio_SetActiveScene"#Lobby",FALSE
	duio_SetActiveScene"#Dialog-Unlimited-NoFragment",TRUE
	duio_InitAllCount"#Dialog-Unlimited-NoFragment"
	duio_SetAllButtonVisible"#Dialog-Unlimited-NoFragment",TRUE
	duio_SetAllImageVisible"#Dialog-Unlimited-NoFragment",TRUE
	duio_SetAllTextVisible"#Dialog-Unlimited-NoFragment",TRUE
	return