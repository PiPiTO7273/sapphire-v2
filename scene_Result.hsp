
*scene_Result_Init

	gosub*UploadAllSavedata

	dim SceneCount
	dim SceneTime
	dim decidecount
	sdim DecideJumpScene
	decidecount=-1

	UpdateProfileDisp

	//アクション
	duio_SetImageVisible duio_ImageID2UIsetID("result_action_layer"),FALSE
	duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_twitter"),FALSE
	duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_savepicture"),FALSE
	duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_retry"),FALSE
	duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_next"),FALSE

	duio_SetImageOutro duio_ImageID2UIsetID("result_status_load"),FALSE
	duio_SetTextOutro duio_TextID2UIsetID("result_status"),FALSE

	duio_InitAllCount"#Common-Profile"
	duio_InitAllCount"#Result"

	dim item:dim lv
	item=curselect_item
	lv=curselect_lv

	//debug ★★★★★★★★★★★★★★★★★★★★★★★
	;curScore=9999999
	;targetscore=1234567
	;dim curJudge,7
	;curJudge(JUDGEID_GOOD)=999
	;objmax=1000
	;dim gaugecapture_buf,350
	;dim scorecapture_buf,objmax
	//★★★★★★★★★★★★★★★★★★★★★★★★★★

	//ジャケット画像
	duio_ChangeImage duio_ImageID2UIsetID("result_artwork"),mapdb_hartwork(item,lv)

	//メタデータ
	duio_ChangeTextBody duio_TextID2UIsetID("result_meta_title"),mapdb_title(item,lv)
	duio_ChangeTextBody duio_TextID2UIsetID("result_meta_artist"),mapdb_artist(item,lv)
	duio_ChangeImage duio_ImageID2UIsetID("result_meta_lvimg"),hdximg(imusicselect_item_normal+mapdb_difficulty(item,lv))
	duio_ChangeTextBody duio_TextID2UIsetID("result_meta_lv"),"Lv. "+mapdb_level(item,lv)

	//スコア
		//今回
		duio_ChangeTextBody duio_TextID2UIsetID("result_score_curscore"),str(curScore)
		duio_ChangeTextBody duio_TextID2UIsetID("result_score_curability"),str(curAbilityScore)

		//自己ベスト
		duio_ChangeTextBody duio_TextID2UIsetID("result_score_mybestscore"),str(mapdb_mybest_score(item,lv))
		duio_ChangeTextBody duio_TextID2UIsetID("result_score_mybestability"),str(mapdb_mybest_pfpoint(item,lv))

		//目標スコア
		duio_ChangeTextBody duio_TextID2UIsetID("result_score_targetscore"),str(targetscore)

		//差分表示
		if curScore-mapdb_mybest_score(item,lv)>=0 {
			duio_ChangeTextBody duio_TextID2UIsetID("result_score_dif_mybest_score"),"+"+str(curScore-mapdb_mybest_score(item,lv))
			duio_SetTextColor duio_TextID2UIsetID("result_score_dif_mybest_score"),$0090FF
		} else {
			duio_ChangeTextBody duio_TextID2UIsetID("result_score_dif_mybest_score"),str(curScore-mapdb_mybest_score(item,lv))
			duio_SetTextColor duio_TextID2UIsetID("result_score_dif_mybest_score"),$FF00A1
		}
		if curScore-targetscore>=0 {
			duio_ChangeTextBody duio_TextID2UIsetID("result_score_dif_target_score"),"+"+str(curScore-targetscore)
			duio_SetTextColor duio_TextID2UIsetID("result_score_dif_target_score"),$0090FF
		} else {
			duio_ChangeTextBody duio_TextID2UIsetID("result_score_dif_target_score"),str(curScore-targetscore)
			duio_SetTextColor duio_TextID2UIsetID("result_score_dif_target_score"),$FF00A1
		}
		if curAbilityScore-mapdb_mybest_pfpoint(item,lv)>=0.0 {
			duio_ChangeTextBody duio_TextID2UIsetID("result_score_dif_mybest_ability"),"+"+strf("%.2f pts",curAbilityScore-mapdb_mybest_pfpoint(item,lv))
			duio_SetTextColor duio_TextID2UIsetID("result_score_dif_mybest_ability"),$0090FF
		} else {
			duio_ChangeTextBody duio_TextID2UIsetID("result_score_dif_mybest_ability"),strf("%.2f pts",curAbilityScore-mapdb_mybest_pfpoint(item,lv))
			duio_SetTextColor duio_TextID2UIsetID("result_score_dif_mybest_ability"),$FF00A1
		}

	//判定の割合
		ddim numjudgeratio,7
		repeat 7
			numjudgeratio(cnt)=double(curJudge(cnt))/double(objmax)
		loop

		//グラフの準備
			//位置
				dim numjudgeratio_graphpos,5,2
				numjudgeratio_graphpos(JUDGEID_EXCELLENT,0)=duio_GetImagePositionX(duio_ImageID2UIsetID(("result_judgedetail_numexcellent_graph")))
				numjudgeratio_graphpos(JUDGEID_EXCELLENT,1)=duio_GetImagePositionY(duio_ImageID2UIsetID(("result_judgedetail_numexcellent_graph")))
				numjudgeratio_graphpos(JUDGEID_GREAT,0)=duio_GetImagePositionX(duio_ImageID2UIsetID(("result_judgedetail_numgreat_graph")))
				numjudgeratio_graphpos(JUDGEID_GREAT,1)=duio_GetImagePositionY(duio_ImageID2UIsetID(("result_judgedetail_numgreat_graph")))
				numjudgeratio_graphpos(JUDGEID_GOOD,0)=duio_GetImagePositionX(duio_ImageID2UIsetID(("result_judgedetail_numgood_graph")))
				numjudgeratio_graphpos(JUDGEID_GOOD,1)=duio_GetImagePositionY(duio_ImageID2UIsetID(("result_judgedetail_numgood_graph")))
				numjudgeratio_graphpos(JUDGEID_BAD,0)=duio_GetImagePositionX(duio_ImageID2UIsetID(("result_judgedetail_numbad_graph")))
				numjudgeratio_graphpos(JUDGEID_BAD,1)=duio_GetImagePositionY(duio_ImageID2UIsetID(("result_judgedetail_numbad_graph")))
				numjudgeratio_graphpos(JUDGEID_MISS,0)=duio_GetImagePositionX(duio_ImageID2UIsetID(("result_judgedetail_nummiss_graph")))
				numjudgeratio_graphpos(JUDGEID_MISS,1)=duio_GetImagePositionY(duio_ImageID2UIsetID(("result_judgedetail_nummiss_graph")))
			//サイズ
				dim numjudgeratio_graphsize,5,2
				numjudgeratio_graphsize(JUDGEID_EXCELLENT,1)=duio_GetImageHeight(duio_ImageID2UIsetID(("result_judgedetail_numexcellent_graph")))
				numjudgeratio_graphsize(JUDGEID_EXCELLENT,0)=duio_GetImageWidth(duio_ImageID2UIsetID(("result_judgedetail_numexcellent_graph")))
				numjudgeratio_graphsize(JUDGEID_GREAT,0)=duio_GetImageWidth(duio_ImageID2UIsetID(("result_judgedetail_numgreat_graph")))
				numjudgeratio_graphsize(JUDGEID_GREAT,1)=duio_GetImageHeight(duio_ImageID2UIsetID(("result_judgedetail_numgreat_graph")))
				numjudgeratio_graphsize(JUDGEID_GOOD,0)=duio_GetImageWidth(duio_ImageID2UIsetID(("result_judgedetail_numgood_graph")))
				numjudgeratio_graphsize(JUDGEID_GOOD,1)=duio_GetImageHeight(duio_ImageID2UIsetID(("result_judgedetail_numgood_graph")))
				numjudgeratio_graphsize(JUDGEID_BAD,0)=duio_GetImageWidth(duio_ImageID2UIsetID(("result_judgedetail_numbad_graph")))
				numjudgeratio_graphsize(JUDGEID_BAD,1)=duio_GetImageHeight(duio_ImageID2UIsetID(("result_judgedetail_numbad_graph")))
				numjudgeratio_graphsize(JUDGEID_MISS,0)=duio_GetImageWidth(duio_ImageID2UIsetID(("result_judgedetail_nummiss_graph")))
				numjudgeratio_graphsize(JUDGEID_MISS,1)=duio_GetImageHeight(duio_ImageID2UIsetID(("result_judgedetail_nummiss_graph")))

		//テキスト反映
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numexcellent"),str(curJudge(JUDGEID_EXCELLENT))
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numgreat"),str(curJudge(JUDGEID_GREAT))
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numgood"),str(curJudge(JUDGEID_GOOD))
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numbad"),str(curJudge(JUDGEID_BAD))
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_nummiss"),str(curJudge(JUDGEID_MISS))
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numearly"),str(curJudge(JUDGEID_EARLY))
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numlate"),str(curJudge(JUDGEID_LATE))

			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numexcellent_ratio"),str(numjudgeratio(JUDGEID_EXCELLENT)*100.0)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numgreat_ratio"),str(numjudgeratio(JUDGEID_GREAT)*100.0)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numgood_ratio"),str(numjudgeratio(JUDGEID_GOOD)*100.0)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numbad_ratio"),str(numjudgeratio(JUDGEID_BAD)*100.0)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_nummiss_ratio"),str(numjudgeratio(JUDGEID_MISS)*100.0)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numearly_ratio"),str(numjudgeratio(JUDGEID_EARLY)*100.0)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_numlate_ratio"),str(numjudgeratio(JUDGEID_LATE)*100.0)
	//平均オフセット
		ddim avgOffset
		if RecentJudgeCount==0 {
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_avgoffset"),"-"
		} else {
			repeat RecentJudgeCount
				avgOffset+double(RecentJudgeGap(cnt))
			loop
			avgOffset=avgOffset/double(RecentJudgeCount)
			duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_avgoffset"),strf("%.2f ms",avgOffset)
		}

	//最大判定精度
		duio_ChangeTextBody duio_TextID2UIsetID("result_judgedetail_maxjudgeacc"),strf("%.2f ms",1000.0/limitf(MaxSubThreadFPS,1.0))

	//その他の情報（表）
		//クリアの種類
			//自己ベスト（文字列→定数）
				dim mybestClearType
				mybestClearType=CLEARTYPE_NODATA
				foreach common_cleartype
					if common_cleartype(cnt)==mapdb_mybest_cleartype(item,lv) :mybestClearType=cnt:break
				loop

				if GameplayMode==GAMEMODE_MISSION&mission_type(currentSetMission)=="CLEAR" {
					dim missionClearType
					missionClearType=CLEARTYPE_NODATA
					foreach common_cleartype
						if common_cleartype(cnt)==mission_typeprm(currentSetMission) :missionClearType=cnt:break
					loop
				}

			//今回
				dim curClearType
				curClearType=CLEARTYPE_FAILED
				if pfreg_GaugeType==PLAYOPT_ID_GAUGE_ASSIST&curGauge>=70.0 :curClearType=CLEARTYPE_ASSIST
				if pfreg_GaugeType==PLAYOPT_ID_GAUGE_NORMAL&curGauge>=70.0 :curClearType=CLEARTYPE_CLEAR
				if pfreg_GaugeType==PLAYOPT_ID_GAUGE_EXPERT&curGauge>0.0 :curClearType=CLEARTYPE_EXPERT
				if pfreg_GaugeType==PLAYOPT_ID_GAUGE_MADNESS&curGauge>0.0 :curClearType=CLEARTYPE_MADNESS
				if curCombo==objmax :curClearType=CLEARTYPE_FC
				if curJudge(JUDGEID_EXCELLENT)==objmax :curClearType=CLEARTYPE_ALLEX

			
		duio_ChangeTextBody duio_TextID2UIsetID("result_others_curcleartype"),common_cleartype(curClearType)
		duio_ChangeTextBody duio_TextID2UIsetID("result_others_curmaxcombo"),strf("%04d",maxCombo)
		duio_ChangeTextBody duio_TextID2UIsetID("result_others_mybestcleartype"),common_cleartype(mybestClearType)
		duio_ChangeTextBody duio_TextID2UIsetID("result_others_mybestmaxcombo"),strf("%04d",mapdb_mybest_maxcombo(item,lv))

	//送信用データ
		dim saveScore:dim saveJudge:dim saveMaxCombo:dim saveClearType:dim saveGaugeCapture:sdim saveScoreCapture:ddim saveability
		if curScore>mapdb_mybest_score(item,lv) {
			//自己ベストスコアを更新
			duio_SetImageVisible duio_ImageID2UIsetID("result_score_new_score"),TRUE
			saveScore=curScore

			//判定
			repeat 7
				saveJudge(cnt)=curJudge(cnt)
			loop

			//スコアグラフ
			saveScoreCapture="K"
			repeat objmax
				saveScoreCapture+scorecapture_buf(cnt)
			loop
		} else {
			//自己ベストスコアを下回った
			duio_SetImageVisible duio_ImageID2UIsetID("result_score_new_score"),FALSE
			saveScore=mapdb_mybest_score(item,lv)

			//判定
			repeat 7
				saveJudge(cnt)=mapdb_mybest_judgedetail(item,lv,cnt)
			loop

			//スコアグラフ
			saveScoreCapture=mapdb_mybest_score4binhex(item,lv)
		}
		if maxcombo>mapdb_mybest_maxcombo(item,lv) {
			//自己ベストの最大コンボを更新
			duio_SetImageVisible duio_ImageID2UIsetID("result_others_new_maxcombo"),TRUE
			saveMaxCombo=maxcombo
		} else {
			//自己ベストの最大コンボを下回った
			duio_SetImageVisible duio_ImageID2UIsetID("result_others_new_maxcombo"),FALSE
			saveMaxCombo=mapdb_mybest_maxcombo(item,lv)
		}
		if curClearType>mybestClearType {
			//自己ベストのクリアランプを更新
			duio_SetImageVisible duio_ImageID2UIsetID("result_others_new_cleartype"),TRUE
			saveClearType=curClearType

			//ゲージ推移
			repeat 350
				saveGaugeCapture(cnt)=gaugecapture_buf(cnt)
				if saveGaugeCapture(cnt)<0|saveGaugeCapture(cnt)>100 :saveGaugeCapture(cnt)=0
			loop
		} else {
			//自己ベストのクリアランプを更新しない
			duio_SetImageVisible duio_ImageID2UIsetID("result_others_new_cleartype"),FALSE
			saveClearType=mybestClearType

			//ゲージ推移
			repeat 350
				saveGaugeCapture(cnt)=peek(mapdb_mybest_gauge4binhex(item,lv),cnt)
				if saveGaugeCapture(cnt)<0|saveGaugeCapture(cnt)>100 :saveGaugeCapture(cnt)=0
			loop
		}
		if curAbilityScore>mapdb_mybest_pfpoint(item,lv) {
			//自己ベストの能力値を更新
			duio_SetImageVisible duio_ImageID2UIsetID("result_score_new_ability"),TRUE
			saveability=curAbilityScore
		} else {
			//自己ベストの能力値を下回った
			duio_SetImageVisible duio_ImageID2UIsetID("result_score_new_ability"),FALSE
			saveability=mapdb_mybest_pfpoint(item,lv)
		}

	//処理フラグ
		dim recordsend_flag
		dim irsend_flag
		dim recordload_flag
		dim irload_flag

		if pfreg_Placement==PLAYOPT_ID_OBJPLACE_SPEEDFIX :recordsend_flag=TRUE:irsend_flag=TRUE
		if AutoPlayFlag :recordsend_flag=TRUE:irsend_flag=TRUE

	//ミッションクリア処理
	if GameplayMode==GAMEMODE_MISSION {
		dim reward_windowflag
		reward_windowflag=FALSE

		switch mission_type(currentSetMission)
			case"SCORE"
				if curScore>=int(mission_typeprm(currentSetMission)) {
					//クリア
					gosub*scene_Mission_GetReward
					reward_windowflag=TRUE
				} else {
					//失敗
					mission_progress(currentSetMission)++
				}
			swbreak
			case"CLEAR"
				if curClearType>=missionClearType {
					//クリア
					gosub*scene_Mission_GetReward
					reward_windowflag=TRUE
				} else {
					//失敗
					mission_progress(currentSetMission)++
				}
			swbreak
		swend

		if mission_progress(currentSetMission)==5 {
			gosub*scene_Mission_GetReward
			reward_windowflag=TRUE
		}

		PortalWebSvc_SendProfile PlayerID,PlayerPassword,"XODA.MISSION.PROGRESS."+currentSetMission,str(mission_progress(currentSetMission))
	}

	//スコアランク判定
		duio_ChangeImage duio_ImageID2UIsetID("result_scorerank"),hdximg(iresult_rank_f-limit(curScore/1000000,0,9))

	//グラフ画像生成
		SetDrawScreen hdximg(iresult_gaugegraph)
		ClearDrawScreen

		SetDrawBlendMode DX_BLENDMODE_ALPHA,256
		ddim x:ddim y
		repeat 350-1,1
			x(0)=double(0+cnt*2)
			y(0)=double(100-gaugecapture_buf(cnt-1))
			x(1)=x(0)+2.0
			y(1)=double(100-gaugecapture_buf(cnt))
			DrawLineAA d2f(x(0)),d2f(y(0)),d2f(x(1)),d2f(y(1)),0,d2f(2.0)
		loop

	duio_SetAllImageOutro"#Shutter"
	duio_PlaySound"shutter_out"
	if curClearType!CLEARTYPE_FAILED {
		duio_PlaySound"result_0"
		duio_PlaySound"buzz"

		duio_PlaySound"voice_0_"+rnd(3)
	} else {
		duio_PlaySound"result_1"

		duio_PlaySound"voice_1_"+rnd(3)
	}

	return

*scene_Result

	DrawCommonBackground SceneCount

	SetDrawScreen hcommonLayer

	dim shapemovepos:shapemovepos=-(1000.0/duio_GetFPS()*SceneCount)/15
	SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
	DrawGraph shapemovepos\1920,20,hdximg(icommon_ironline),TRUE
	DrawGraph shapemovepos\1920+1920,20,hdximg(icommon_ironline),TRUE

	duio_Draw"#Result"
	duio_Draw"#Common-Profile"

	//判定の割合グラフ (excellent,great,good,bad,miss)
	SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
	repeat 5
		setease 0,int(double(numjudgeratio_graphsize(cnt,0))*numjudgeratio(cnt)),ease_cubic_inout
		DrawExtendGraph numjudgeratio_graphpos(cnt,0),numjudgeratio_graphpos(cnt,1),numjudgeratio_graphpos(cnt,0)+getease(1000.0/duio_GetFPS()*SceneCount-800,1000),numjudgeratio_graphpos(cnt,1)+numjudgeratio_graphsize(cnt,1),hdximg(iresult_ratio_excellent+cnt),TRUE
	loop

	//ゲージの推移
	/*ddim x:ddim y
	setease 0,350-1,ease_cubic_out
	repeat getease(1000.0/duio_GetFPS()*SceneCount-500,1000),1
		x(0)=double(duio_GetImagePositionX(duio_ImageID2UIsetID("result_bg_gaugegraph"))+cnt*2)
		y(0)=double(duio_GetImagePositionY(duio_ImageID2UIsetID("result_bg_gaugegraph"))+44+100-gaugecapture_buf(cnt-1))
		x(1)=x(0)+2.0
		y(1)=double(duio_GetImagePositionY(duio_ImageID2UIsetID("result_bg_gaugegraph"))+44+100-gaugecapture_buf(cnt))
		DrawLineAA d2f(x(0)),d2f(y(0)),d2f(x(1)),d2f(y(1)),0,d2f(2.0)
	loop*/
	setease 0,700,ease_cubic_out
	dim EasePosX:dim EasePosY:dim EaseWidth
	EasePosX=duio_GetImagePositionX(duio_ImageID2UIsetID("result_bg_gaugegraph"))
	EasePosY=duio_GetImagePositionY(duio_ImageID2UIsetID("result_bg_gaugegraph"))
	EaseWidth=getease(SceneTime-500,1000)
	DrawRectExtendGraph EasePosX,EasePosY+44,EasePosX+EaseWidth,EasePosY+44+100,0,0,EaseWidth,100,hdximg(iresult_gaugegraph),TRUE

	if reward_windowflag {
		duio_Draw"#Result-MissionReward"
		/*if SceneTime>3000&duio_GetImageOutroFlag(duio_ImageID2UIsetID("result_missionreward_bg"))==FALSE {
			duio_SetAllButtonOutro"#Result-MissionReward"
			duio_SetAllImageOutro"#Result-MissionReward"
			duio_SetAllTextOutro"#Result-MissionReward"
		}*/
	}

	//シャッター
	duio_Draw"#Shutter"

	if SceneTime<3000 {
		//接続中
		duio_ChangeImage duio_ImageID2UIsetID("result_status_load"),hdximgarr_load_connect(SceneTime/20\28)
	}

	if SceneTime>1500&recordsend_flag==FALSE {
		PortalWebSvc_UploadRecord map_uuid(item,lv),PlayerID,saveScore,saveJudge,saveMaxCombo,common_cleartype(saveClearType),saveGaugeCapture,saveScoreCapture,saveability
		recordsend_flag=TRUE
	}
	if SceneTime>1800&irsend_flag==FALSE {
		PortalWebSvc_UploadIR map_uuid(item,lv),PlayerID,saveScore
		irsend_flag=TRUE
	}
	if SceneTime>2100&recordload_flag==FALSE {
		;gosub*syncRecords
		recordload_flag=TRUE
	}
	if SceneTime>2500&irload_flag==FALSE {
		;gosub*syncIR
		irload_flag=TRUE

		duio_SetImageOutro duio_ImageID2UIsetID("result_status_load"),TRUE
		duio_SetTextOutro duio_TextID2UIsetID("result_status"),TRUE

		duio_SetImageVisible duio_ImageID2UIsetID("result_action_layer"),TRUE
		duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_twitter"),TRUE
		duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_savepicture"),TRUE
		duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_retry"),TRUE
		duio_SetButtonVisible duio_ButtonID2UIsetID("result_action_next"),TRUE
	}

	if duio_GetButtonEventStack()!"" :gosub*scene_Result_eventStack

	if decidecount!-1 :decidecount+FrameTime

	if decidecount>1000 {
		duio_StopSound"result_0"
		duio_StopSound"result_1"

		switch DecideJumpScene
			case"next"
				switch GameplayMode
					case GAMEMODE_FREEPLAY
						curSceneTag="#MusicSelect"
						gosub*scene_MusicSelect_Init
					swbreak
					case GAMEMODE_MISSION
						curSceneTag="#Mission"
						gosub*scene_Mission_Init
						duio_SetAllImageOutro"#Shutter"
						duio_PlaySound"shutter_out"
					swbreak
				swend
			swbreak
		swend
	}

	return

*scene_Result_eventstack

	switch duio_GetButtonEventStack()

		//次へ
		case"next"
			if decidecount==-1 {
				decidecount=0:DecideJumpScene="next"
				gosub*onshutter
			}
		swbreak

		//報酬の通知を閉じる
		case"missionreward_close"
			duio_SetAllButtonOutro"#Result-MissionReward"
			duio_SetAllImageOutro"#Result-MissionReward"
			duio_SetAllTextOutro"#Result-MissionReward"
		swbreak

		//もう一度
		case"retry"
			curSceneTag="#Gameplay"
			gosub*scene_Gameplay_free
			gosub*scene_Gameplay_Init
		swbreak

		//スクショ
		case"savepicture"
			sdim savepic_path:savepic_path="sapphire-screenshot-"+strf("%04d%02d%02d%02d%02d%02d",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6))+".png"
			chdir dir_desktop
			SetDrawScreen hcommonLayer
			SaveDrawScreenToPNG 0,0,BufWidth,BufHeight,savepic_path
			exec savepic_path,16
			chdir dir_default
		swbreak

		//Twitterシェア
		case"twitter"
			exec"http://twitter.com/share?text="+pfreg_dispname+"さんが楽曲「"+mapdb_title(curselect_item,curselect_lv)+"」の難易度"+difid2difstr(curselect_lv)+"でスコア"+strf("%08d",curScore)+"を獲得しました！%0a%0aあなたもSapphireをプレイしてみませんか？%0a%23sapphire%0a&url=http://puoro.ml",16
		swbreak

		//ゲーム終了
		case"game.exit":goto*Exit:swbreak

	swend

	return