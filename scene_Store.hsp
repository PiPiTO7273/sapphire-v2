 
*scene_Store_Init

	gosub*UploadAllSavedata

	//duio_SetAllImageOutro"#Shutter"
	duio_InitAllCount"#Store"
	duio_SetAllButtonVisible"#Store",TRUE
	duio_SetAllImageVisible"#Store",TRUE
	duio_SetAllTextVisible"#Store",TRUE
	duio_SetActiveScene"#Store",TRUE

	duio_InitAllCount"#Store-Confirm"
	duio_SetActiveScene"#Store-Confirm",FALSE
	duio_SetAllButtonVisible"#Store-Confirm",FALSE
	duio_SetAllImageVisible"#Store-Confirm",FALSE
	duio_SetAllTextVisible"#Store-Confirm",FALSE

	UpdateProfileDisp

	if BASS_ChannelIsActive(duio_GetHandle("main"))!BASS_ACTIVE_PLAYING {
		duio_PlaySound"main"
	}
	duio_SetVolume"main",1.0

	dim decidecount
	dim show_purchased
	dim curcategory
	dim mapitem_max
	dim SceneCount
	show_purchased=TRUE
	decidecount=-1

	InitStoreItem 0

	return

*scene_Store

	DrawCommonBackground FrameCount

	SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
	DrawGraph 0,0,hdximg(icommon_bglayer),TRUE

	//鉄骨ライン
		dim shapemovepos:shapemovepos=-(1000.0/duio_GetFPS()*FrameCount)/15
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
		DrawGraph shapemovepos\1920,20,hdximg(icommon_ironline),TRUE
		DrawGraph shapemovepos\1920+1920,20,hdximg(icommon_ironline),TRUE

	duio_Draw"#Store"
	duio_Draw"#Common-Profile"

	//スクロール処理
	if (159+210*(mapitem_max/2))>BufHeight-64-192 {
		if insquare(cursorPosX,cursorPosY,760,0,BufWidth,BufHeight)&mapitem_max!0 {
			mapitem_cursorwheel=cursorWheel
			repeat mapitem_max
				duio_SetButtonPositionY mapitem_uisetid(cnt),duio_GetButtonPositionY(mapitem_uisetid(cnt))+mapitem_cursorwheel
			loop
			if duio_GetButtonPositionY(mapitem_uisetid(0))>159&mapitem_cursorwheel>0 {
				mapitem_scrollfix=duio_GetButtonPositionY(mapitem_uisetid(0))-159
				repeat mapitem_max
					duio_SetButtonPositionY mapitem_uisetid(cnt),duio_GetButtonPositionY(mapitem_uisetid(cnt))-mapitem_scrollfix
				loop
			}
			if duio_GetButtonPositionY(mapitem_uisetid(mapitem_max-1))<BufHeight-64-192&mapitem_cursorwheel<0 {
				mapitem_scrollfix=(BufHeight-64-192)-duio_GetButtonPositionY(mapitem_uisetid(mapitem_max-1))
				repeat mapitem_max
					duio_SetButtonPositionY mapitem_uisetid(cnt),duio_GetButtonPositionY(mapitem_uisetid(cnt))+mapitem_scrollfix
				loop
			}
		}
		
		//スクロール位置を特定
			if mapitem_max!0 {
				SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
				mapitem_scrollpos=duio_GetButtonPositionY(mapitem_uisetid(mapitem_max-1))-duio_GetButtonPositionY(mapitem_uisetid(0))-BufHeight+64+192+159
			}

		//スクロールバー
			if insquare(cursorPosX,cursorPosY,1885,140,1885+20,140+710)&(cursorStatus&MOUSE_INPUT_LEFT) :mapitem_scrollclickmode=TRUE
			if mapitem_scrollclickmode {
				cursorPosY=limit(cursorPosY,140,140+710)
				repeat mapitem_max
					duio_SetButtonPositionY mapitem_uisetid(cnt),159+210*(cnt/2)-int(double(mapitem_scrollpos)/700.0*double(cursorPosY-140))
				loop
				if (cursorStatus&MOUSE_INPUT_LEFT)==FALSE :mapitem_scrollclickmode=FALSE
			}
			DrawGraph 1870,130+int(700.0*(-double(duio_GetButtonPositionY(mapitem_uisetid(0))-159)/double(mapitem_scrollpos))),hdximg(imusicselect_itemscroll_circle),TRUE
	}

	//現在のカテゴリ選択
	if decidecount==-1&SceneTime>400 {
		setease 0,256,ease_linear+ease_loop
		SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,getease(SceneTime-400,200)
		DrawGraph 260,309+64*curcategory,hdximg(istore_category),TRUE
	}

	duio_Draw"#Store-Confirm"
	duio_Draw"#Store-LepusError"
	duio_Draw"#Store-Confirm-LillyConv"
	;duio_Draw"#Shutter"

	if duio_GetButtonEventStack()!"" :gosub*scene_Store_eventstack
	if decidecount!-1 :decidecount+FrameTime
	if decidecount>300 {
		ClearMapItem
		gosub*scene_Lobby_Init
		curSceneTag="#Lobby"
		return
	}

	return

*scene_Store_eventstack

	switch duio_GetButtonEventStack()

		//譜面カテゴリ
		case"category_song":InitStoreItem 0:swbreak

		//アイテムカテゴリ
		case"category_item":InitStoreItem 1:swbreak

		//カスタマイズパーツカテゴリ
		case"category_customize":InitStoreItem 2:swbreak

		//購入済み表示スイッチon,off
		case"purchased_onoff"
			if show_purchased {
				show_purchased=0
				duio_ChangeButtonImage duio_ButtonID2UIsetID("store_purchased"),hdximg(istore_show_purchased_off)
			} else {
				show_purchased=1
				duio_ChangeButtonImage duio_ButtonID2UIsetID("store_purchased"),hdximg(istore_show_purchased_on)
			}
			InitStoreItem curcategory
		swbreak

		//戻る
		case"backscene"
			decidecount=0
			duio_SetAllButtonOutro"#Store"
			duio_SetAllImageOutro"#Store"
			duio_SetAllTextOutro"#Store"
			;gosub*onshutter
		swbreak

		//変換
		case"exchange"
			duio_InitAllCount"#Store-Confirm-LillyConv"
			duio_SetActiveScene"#Store-Confirm-LillyConv",TRUE
			duio_SetActiveScene"#Store",FALSE
			duio_SetAllButtonVisible"#Store-Confirm-LillyConv",TRUE
			duio_SetAllImageVisible"#Store-Confirm-LillyConv",TRUE
			duio_SetAllTextVisible"#Store-Confirm-LillyConv",TRUE
			duio_ChangeTextBody duio_TextID2UIsetID("store_lillyconv_value"),str(pfreg_NumLilly)
		swbreak

		//変換 OK
		case"store_lillyconv_ok"
			duio_SetActiveScene"#Store-Confirm-LillyConv",FALSE
			duio_SetActiveScene"#Store",TRUE
			duio_SetAllButtonOutro"#Store-Confirm-LillyConv"
			duio_SetAllImageOutro"#Store-Confirm-LillyConv"
			duio_SetAllTextOutro"#Store-Confirm-LillyConv"

			pfreg_NumLepus+pfreg_NumLilly
			pfreg_NumLilly=0
			UpdateProfileDisp
		swbreak
		//変換 キャンセル
		case"store_lillyconv_cancel"
			duio_SetActiveScene"#Store-Confirm-LillyConv",FALSE
			duio_SetActiveScene"#Store",TRUE
			duio_SetAllButtonOutro"#Store-Confirm-LillyConv"
			duio_SetAllImageOutro"#Store-Confirm-LillyConv"
			duio_SetAllTextOutro"#Store-Confirm-LillyConv"
		swbreak

		//購入確認 OK
		case"store_purchase_ok"
			duio_SetActiveScene"#Store",TRUE
			switch int(strmid(itempush_event,0,1))
				case 0
					//譜面
					strrep itempush_event,"0@",""
					SearchDBfromUUID itempush_event,item,lv

					if pfreg_NumLepus<mapdb_unlocklepus(item,lv) {
						//lepus不足
						ShowLepusError mapdb_unlocklepus(item,lv)
					} else {
						//購入処理
						pfreg_NumLepus-mapdb_unlocklepus(item,lv)
						duio_idsWrite"_MapUnlock."+itempush_event,"TRUE"
						PortalWebSvc_SendProfile PlayerID,PlayerPassword,"XODA.MAP.UNLOCK."+itempush_event,"1"

						duio_SetActiveScene"#Store",TRUE
					}
				swbreak
				case 1
					//アイテム
					strrep itempush_event,"1@",""

					switch itempush_event
						case"ticket1"
							if pfreg_NumLepus<1000 {		//lepus不足
								ShowLepusError 1000
							} else {
								pfreg_NumLepus-1000:pfreg_NumTickets+1
							}
						swbreak
						case"ticket10"
							if pfreg_NumLepus<9000 {		//lepus不足
								ShowLepusError 9000
							} else {
								pfreg_NumLepus-9000:pfreg_NumTickets+10
							}
						swbreak
						case"fragment1"
							if pfreg_NumLepus<300 {		//lepus不足
								ShowLepusError 300
							} else {
								pfreg_NumLepus-300:pfreg_NumFragments+1
							}
						swbreak
						case"fragment5"
							if pfreg_NumLepus<1400 {		//lepus不足
								ShowLepusError 1400
							} else {
								pfreg_NumLepus-1400:pfreg_NumFragments+5
							}
						swbreak
						case"fragment10"
							if pfreg_NumLepus<2700 {		//lepus不足
								ShowLepusError 2700
							} else {
								pfreg_NumLepus-2700:pfreg_NumFragments+10
							}
						swbreak
					swend
				swbreak
				case 2
					//アイテム
					strrep itempush_event,"2@",""

					switch strmid(itempush_event,0,1)
						case"n"
							strrep itempush_event,"n",""
							if pfreg_NumLepus<customize_note_price(int(itempush_event)) {
								ShowLepusError customize_note_price(int(itempush_event))
							} else {
								pfreg_NumLepus-customize_note_price(int(itempush_event))
								customize_note_unlockflag(int(itempush_event))=TRUE
								duio_idsWrite"_Customize.n"+itempush_event,"TRUE"
								PortalWebSvc_SendProfile PlayerID,PlayerPassword,"XODA.CUSTOMIZE.UNLOCK.n"+itempush_event,"1"
							}
						swbreak
						case"b"
							strrep itempush_event,"b",""
							if pfreg_NumLepus<customize_bg_price(int(itempush_event)) {
								ShowLepusError customize_bg_price(int(itempush_event))
							} else {
								pfreg_NumLepus-customize_bg_price(int(itempush_event))
								customize_bg_unlockflag(int(itempush_event))=TRUE
								duio_idsWrite"_Customize.b"+itempush_event,"TRUE"
								PortalWebSvc_SendProfile PlayerID,PlayerPassword,"XODA.CUSTOMIZE.UNLOCK.b"+itempush_event,"1"
							}
						swbreak
					swend
				swbreak
			swend

			//確認画面を閉じる
			duio_SetActiveScene"#Store-Confirm",FALSE
			duio_SetAllButtonOutro"#Store-Confirm"
			duio_SetAllImageOutro"#Store-Confirm"
			duio_SetAllTextOutro"#Store-Confirm"

			//更新
			UpdateProfileDisp
			InitStoreItem curcategory
		swbreak
		//購入確認 キャンセル
		case"store_purchase_cancel"
			duio_SetActiveScene"#Store-Confirm",FALSE
			duio_SetActiveScene"#Store",TRUE
			duio_SetAllButtonOutro"#Store-Confirm"
			duio_SetAllImageOutro"#Store-Confirm"
			duio_SetAllTextOutro"#Store-Confirm"
		swbreak

		//Lepus不足 OK
		case"store_error_ok"
			duio_SetActiveScene"#Store-LepusError",FALSE
			duio_SetActiveScene"#Store",TRUE
			duio_SetAllButtonOutro"#Store-LepusError"
			duio_SetAllImageOutro"#Store-LepusError"
			duio_SetAllTextOutro"#Store-LepusError"
		swbreak

		//ゲーム終了
		case"game.exit":goto*Exit:swbreak

		default
			//項目選択
			sdim itempush_event
			itempush_event=duio_GetButtonEventStack()

			switch int(strmid(itempush_event,0,1))
				case 0
					//譜面
					strrep itempush_event,"0@",""
					SearchDBfromUUID itempush_event,item,lv

					if IsUnlocked(item,lv) :return

					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_type"),"楽曲 / 譜面難易度"
					duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),mapdb_hartwork(item,lv)
					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),mapdb_title(item,lv)
					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),mapdb_artist(item,lv)
					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm2"),difid2difstr(lv)+" Lv."+mapdb_level(item,lv)

					if lv==0 :duio_SetTextColor duio_TextID2UIsetID("store_confirm_prm2"),$33FFC7
					if lv==1 :duio_SetTextColor duio_TextID2UIsetID("store_confirm_prm2"),$FFA033
					if lv==2 :duio_SetTextColor duio_TextID2UIsetID("store_confirm_prm2"),$FF3385

					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),str(mapdb_unlocklepus(item,lv))
				swbreak
				case 1
					//アイテム
					strrep itempush_event,"1@",""

					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_type"),"アイテム"
					duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm2"),""

					switch itempush_event
						case"ticket1"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_ticket)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),"チャレンジチケット"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"1枚"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),"1000"
						swbreak
						case"ticket10"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_ticket)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),"チャレンジチケット"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"10枚"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),"9000"
						swbreak
						case"fragment1"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_fragment_0)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),"音色のかけら"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"1個"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),"300"
						swbreak
						case"fragment5"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_fragment_1)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),"音色のかけら"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"5個"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),"1400"
						swbreak
						case"fragment10"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_fragment_1)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),"音色のかけら"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"10個"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),"2700"
						swbreak
					swend
				swbreak
				case 2
					//カスタマイズパーツ
					strrep itempush_event,"2@",""

					switch strmid(itempush_event,0,1)

						case"n"
							strrep itempush_event,"n",""
							if customize_note_unlockflag(int(itempush_event)) :return

							//アイコン生成
							SetDrawScreen hdximg(istore_icon_tmp)
							ClearDrawScreen
								SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
								DrawExtendGraph 43,35,43+64,35+64,customize_note_hhold(int(itempush_event)),TRUE
								DrawExtendGraph 43,35+64,43+64,35+64+16,customize_note_hsingle(int(itempush_event)),TRUE
		
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_type"),"カスタマイズパーツ"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_tmp)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),customize_note_name(int(itempush_event))
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"音符"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm2"),""
		
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),str(customize_note_price(int(itempush_event)))
						swbreak

						case"b"
							strrep itempush_event,"b",""
							if customize_bg_unlockflag(int(itempush_event)) :return

							//アイコン生成
							SetDrawScreen hdximg(istore_icon_tmp)
							ClearDrawScreen
								SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
								DrawRectExtendGraph 561-550,410-410,561+128-550,410+150-410,0,256,256,1024-256,customize_bg_himg(int(itempush_event)),TRUE
		
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_type"),"カスタマイズパーツ"
							duio_ChangeImage duio_ImageID2UIsetID("store_confirm_img"),hdximg(istore_icon_tmp)
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm0"),customize_bg_name(int(itempush_event))
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm1"),"背景"
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_prm2"),""
		
							duio_ChangeTextBody duio_TextID2UIsetID("store_confirm_price"),str(customize_bg_price(int(itempush_event)))
						swbreak

					swend
				swbreak
			swend

			itempush_event=duio_GetButtonEventStack()

			duio_InitAllCount"#Store-Confirm"
			duio_SetActiveScene"#Store-Confirm",TRUE
			duio_SetActiveScene"#Store",FALSE
			duio_SetAllButtonVisible"#Store-Confirm",TRUE
			duio_SetAllImageVisible"#Store-Confirm",TRUE
			duio_SetAllTextVisible"#Store-Confirm",TRUE
		swbreak

	swend

	return

#deffunc InitStoreItem int _category

	curcategory=_category

	ClearMapItem

	//ボタン作成
	dim mapitem_max
	dim mapitem_himg
	dim mapitem_uisetid
	dim mapitem_cursorwheel
	dim mapitem_scrollfix
	dim mapitem_scrollpos
	dim mapitem_scrollclickmode

	switch _category

		case 0
			//未解禁の楽曲
			dim item:dim lv

			repeat mapgroup_max*3

				item=cnt/3:lv=cnt\3

				if map_uuid(item,lv)=="*" :continue
				if mapdb_unlocklepus(item,lv)>0 {
					if IsUnlocked(item,lv)==FALSE|show_purchased {
						//ボタン画像作成
							mapitem_himg(mapitem_max)=MakeScreen(478,192,TRUE)
							SetDrawScreen mapitem_himg(mapitem_max)

							//背景・ジャケット画像
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
							DrawGraph 0,0,hdximg(istore_item_map),TRUE
							DrawExtendGraph 28,26,28+100,26+100,mapdb_hartwork(item,lv),FALSE

							//曲名
							SetDrawBlendMode DX_BLENDMODE_ALPHA,256
							DrawStringToHandle 904-760,53,mapdb_title(item,lv),$000000,duio_GetFontHandle("rom1cm20")
							DrawStringToHandle 904-760,85,mapdb_artist(item,lv),$000000,duio_GetFontHandle("rom1cm14")

							//難易度
							DrawStringToHandle 866-760,145,"Lv. "+str(mapdb_level(item,lv)),$000000,duio_GetFontHandle("ps16")
							DrawGraph 788-760,131,hdximg(imusicselect_item_normal+lv),TRUE

							//値段
							DrawStringToHandle 1115-760,145,str(mapdb_unlocklepus(item,lv))+" Lepus",$000000,duio_GetFontHandle("ps16")

							//購入済み
							if IsUnlocked(item,lv) {
								SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
								DrawGraph 1100-760,20,hdximg(istore_item_purchased),TRUE
							}
						
						//duioに登録
							duio_CreateButton"#Store","0@"+map_uuid(item,lv),mapitem_himg(mapitem_max),760+500*(mapitem_max\2),159+210*(mapitem_max/2),478,192
							mapitem_uisetid(mapitem_max)=stat
							duio_AddInButtonAnimation_Zoom mapitem_uisetid(mapitem_max),300+35*mapitem_max,350,0,ease_cubic_out
							duio_SetButtonEvent mapitem_uisetid(mapitem_max),"0@"+map_uuid(item,lv)
							duio_SetButtonPushSound mapitem_uisetid(mapitem_max),"pushbutton"

							if IsUnlocked(item,lv) {
								duio_SetButtonEnable mapitem_uisetid(mapitem_max),FALSE
							} else {
								duio_SetButtonEnable mapitem_uisetid(mapitem_max),TRUE
							}

						mapitem_max++
					}
				}
			loop
		swbreak

		case 1
			//アイテム
			CreateStoreItem"1@ticket1",hdximg(istore_ticket1)
			CreateStoreItem"1@ticket10",hdximg(istore_ticket10)
			CreateStoreItem"1@fragment1",hdximg(istore_fragment1)
			CreateStoreItem"1@fragment5",hdximg(istore_fragment5)
			CreateStoreItem"1@fragment10",hdximg(istore_fragment10)
		swbreak

		case 2
			//カスタマイズパーツ
			repeat customize_note_max

				if customize_note_price(cnt)>0 {
					if customize_note_unlockflag(cnt)==FALSE|show_purchased {
						//ボタン画像作成
						mapitem_himg(mapitem_max)=MakeScreen(478,192,TRUE)
						SetDrawScreen mapitem_himg(mapitem_max)

						//背景・ジャケット画像
						SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
						DrawGraph 0,0,hdximg(istore_item_customize_note),TRUE
						DrawExtendGraph 806-760,354-318,806-760+64,354-318+64,customize_note_hhold(cnt),TRUE
						DrawExtendGraph 806-760,354-318+64,806-760+64,354-318+64+16,customize_note_hsingle(cnt),TRUE

						//名前
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 904-760,53,customize_note_name(cnt),$000000,duio_GetFontHandle("rom1cm20")

						//値段
						DrawStringToHandle 1115-760,145,str(customize_note_price(cnt))+" Lepus",$000000,duio_GetFontHandle("ps16")

						//購入済み
						if customize_note_unlockflag(cnt) {
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
							DrawGraph 1100-760,20,hdximg(istore_item_purchased),TRUE
						}

						//duioに登録
						duio_CreateButton"#Store","2@n"+cnt,mapitem_himg(mapitem_max),760+500*(mapitem_max\2),159+210*(mapitem_max/2),478,192
						mapitem_uisetid(mapitem_max)=stat
						duio_AddInButtonAnimation_Zoom mapitem_uisetid(mapitem_max),300+35*mapitem_max,350,0,ease_cubic_out
						duio_SetButtonEvent mapitem_uisetid(mapitem_max),"2@n"+cnt
						duio_SetButtonPushSound mapitem_uisetid(mapitem_max),"pushbutton"

						if customize_note_unlockflag(cnt) {
							duio_SetButtonEnable mapitem_uisetid(mapitem_max),FALSE
						} else {
							duio_SetButtonEnable mapitem_uisetid(mapitem_max),TRUE
						}

						mapitem_max++
					}
				}

			loop

			repeat customize_bg_max

				if customize_bg_price(cnt)>0 {
					if customize_bg_unlockflag(cnt)==FALSE|show_purchased {
						//ボタン画像作成
						mapitem_himg(mapitem_max)=MakeScreen(478,192,TRUE)
						SetDrawScreen mapitem_himg(mapitem_max)

						//背景・ジャケット画像
						SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
						DrawGraph 0,0,hdximg(istore_item_customize_note),TRUE
						DrawRectExtendGraph 98-60,72-46,98-60+80,72-46+100,0,256,256,1024-256,customize_bg_himg(cnt),TRUE

						//名前
						SetDrawBlendMode DX_BLENDMODE_ALPHA,256
						DrawStringToHandle 904-760,53,customize_bg_name(cnt),$000000,duio_GetFontHandle("rom1cm20")

						//値段
						DrawStringToHandle 1115-760,145,str(customize_bg_price(cnt))+" Lepus",$000000,duio_GetFontHandle("ps16")

						//購入済み
						if customize_bg_unlockflag(cnt) {
							SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
							DrawGraph 1100-760,20,hdximg(istore_item_purchased),TRUE
						}

						//duioに登録
						duio_CreateButton"#Store","2@b"+cnt,mapitem_himg(mapitem_max),760+500*(mapitem_max\2),159+210*(mapitem_max/2),478,192
						mapitem_uisetid(mapitem_max)=stat
						duio_AddInButtonAnimation_Zoom mapitem_uisetid(mapitem_max),300+35*mapitem_max,350,0,ease_cubic_out
						duio_SetButtonEvent mapitem_uisetid(mapitem_max),"2@b"+cnt
						duio_SetButtonPushSound mapitem_uisetid(mapitem_max),"pushbutton"

						if customize_bg_unlockflag(cnt) {
							duio_SetButtonEnable mapitem_uisetid(mapitem_max),FALSE
						} else {
							duio_SetButtonEnable mapitem_uisetid(mapitem_max),TRUE
						}

						mapitem_max++
					}
				}

			loop
		swbreak

	swend
	return

#deffunc CreateStoreItem str _event,int _himg
	//ボタン画像作成
	mapitem_himg(mapitem_max)=MakeScreen(478,192,TRUE)
	SetDrawScreen mapitem_himg(mapitem_max)

	//背景・ジャケット画像
	SetDrawBlendMode DX_BLENDMODE_PMA_ALPHA,256
	DrawGraph 0,0,_himg,TRUE

	//duioに登録
	duio_CreateButton"#Store",_event,mapitem_himg(mapitem_max),760+500*(mapitem_max\2),159+210*(mapitem_max/2),478,192
	mapitem_uisetid(mapitem_max)=stat
	duio_AddInButtonAnimation_Zoom mapitem_uisetid(mapitem_max),300+35*mapitem_max,350,0,ease_cubic_out
	duio_SetButtonEvent mapitem_uisetid(mapitem_max),_event
	duio_SetButtonPushSound mapitem_uisetid(mapitem_max),"pushbutton"

	mapitem_max++

	return

#deffunc ShowLepusError int _required
	duio_InitAllCount"#Store-LepusError"
	duio_SetActiveScene"#Store",FALSE
	duio_SetActiveScene"#Store-LepusError",TRUE
	duio_SetAllButtonVisible"#Store-LepusError",TRUE
	duio_SetAllImageVisible"#Store-LepusError",TRUE
	duio_SetAllTextVisible"#Store-LepusError",TRUE
	duio_ChangeTextBody duio_TextID2UIsetID("store_error_requiredlepus"),str(_required)
	duio_ChangeTextBody duio_TextID2UIsetID("store_error_shortfalllepus"),str(_required-pfreg_NumLepus)

	return